#######################################
#######################################
#
# CQLD - Clinical Quality Linked Data
#
# Original Author: Doug Whitehead  doug.whitehead@gmail.com
# Date: September 16, 2011
#
#
# Values of "goodness" are relevant for a 3 dimensional filter.
#    Condition - describes the condition of the patient (e.g. where heart failure, asthma, etc)
#    Measure - describes how the hospital responded (e.g. where antibiotic given, vaccinnation, etc)
#    Metric - are qualitative criterion (e.g. where Cleanliness of Hospital is..,  etc)
#
# The 3 dimensional filter is sparse. In other words, Not all Measures and Metrics
# are relevant for a given Condition. And not all Conditions are relevant for a given 
# Measure, etc.  
# 
# Queries 1, 2, and 3 retrieve all instances of Condition, Measure, and Metric.
# Queries 4 and 5 retrieve all instances given one or both of the other dimensions have FIXED values.
#      For example, if one were to select a Condition of interest, it would be FIXED.
# Query 6 retrive all instances of Hospital.
#
# Results from Queries 1,2,3, and 6 should probably be stored in a Hashmap. As the
# dynamic retrieval of an rdfs:label is particularly slow in Virtuoso. As such, it
# radically slows down other queries. Queries 8 and above only fetch Ids for speed.
# Often there will be a slow version (e.g. Query 8 and Query 8slow) provided for convenience.
#
# Values of "goodness" are datum retrieved. Examples of values of "goodness" are:
#      percentage - e.g. the percentage of patience readmitted within 30 days of surgury
#      footnote - a comment, e.g. the sample size was insufficient to be reliable
#
# Queries that return Values of "goodness" are constructed to return all kinds of 
# values of "goodness" (they are OPTIONAL). Different 3D filters will expect different 
# kinds of values of "goodness" (not every kind of value of "goodness" is relevant).
#
# For queries 7 and above, anytime you see an literal/instance, it is meant as a suggestion.
# In other words, the proper literal/instance should be filled into the query at that point.
#
#######################################
# 
# Suggestion for understanding the CQLD domain: execute Query "8slow" below
# 
# Notice how rows that have Conditions and Measures often don't have Metrics
# 
# Notice how rows that have Measures and Metrics often don't have Conditions
# as it may be part of a survey about the Hospital
#
#######################################
#######################################


#######################################
#
# 1) List all ConditionIds and Conditions
#
#      Every ConditionId has exactly one Condition (description).
#      Use this query to populate a Hashmap, so you don't have to use
#      Virtuoso to get the Condition from a ConditionId ever again
#      (as it slows down dynamic retrieval in other queries).
#
#######################################
#
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?conditionId ?condition WHERE {
   ?conditionId a comp:Condition. 
   OPTIONAL { ?conditionId rdfs:comment ?condition. }
} ORDER BY ?conditionId


#######################################
#
# 2) List all MeasureIds and Measures
#
#      Every MeasureId has exactly one Measure (description).
#      Use this query to populate a Hashmap, so you don't have to use
#      Virtuoso to get the Measure from a MeasureId ever again
#      (as it slows down dynamic retrieval in other queries).
# 
#######################################
#
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?measureId ?measure where { 
  {
    ?subClass1 rdfs:subClassOf comp:Measure.
    ?subClass rdfs:subClassOf ?subClass1.
    ?measureId a ?subClass. 
  } UNION {
    ?subClass rdfs:subClassOf comp:Measure.
    ?measureId a ?subClass. 
  } UNION {
    ?measureId a comp:Measure.
  } 
  ?measureId rdfs:comment ?measure.
} ORDER BY ?measureId


#######################################
#
# 3) List all MetricIds and Metrics
#
#      Every MetricId has exactly one Metric (description).
#      Use this query to populate a Hashmap, so you don't have to use
#      Virtuoso to get the Metric from a MetricId ever again
#      (as it slows down dynamic retrieval in other queries).
#
#######################################
#
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?metricId ?metric where { 
  {
    ?subClass1 rdfs:subClassOf comp:Metric.
    ?subClass rdfs:subClassOf ?subClass1.
    ?metricId a ?subClass. 
  } UNION {
    ?subClass rdfs:subClassOf comp:Metric.
    ?metricId a ?subClass.  
  } UNION {
    ?metricId a comp:Metric.
  } 
  ?metricId rdfs:comment ?metric.
} ORDER BY ?metricId


#######################################
#
# 4) Find an Id of one type (ConditionId, MeasureId, MetricId)
# that are relevant if ONE of the other two types are FIXED.
#
#  ?desiredId the info desired
#
#  ?desiredIdPredicate is one of: comp:condition, comp:measure, comp:metric
#  ?givenIdPredicate is one of: comp:condition, comp:measure, comp:metric
#  ?givenId is an object of ?givenIdPredicate
#
#######################################
#
# DO NOT RUN THIS QUERY WITHOUT FILLING IN VALUES FOR 
# ?givenId ?givenIdPedicate, and ?desiredIdPredicate
#
#######################################
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record ?givenIdPredicate ?givenId.
   ?record ?desiredIdPredicate ?desiredId.
} ORDER BY ?desiredId

#####################
#
# 4.1 through 4.6 below are sample queries that can be run without modification
#
#####################

#
# 4.1) What are the Measures that can be used in conjunction with Conditon 9?
#      (when the Outpatient has AMI/Chest Pain)
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:condition <http://healthdata.gov/id/condition/9>.
   ?record comp:measure ?desiredId.
} ORDER BY ?desiredId

#
# 4.2) What are the Metrics that can be used in conjunction with Conditon 9?
#      (when the Outpatient has AMI/Chest Pain)
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:condition <http://healthdata.gov/id/condition/9>.
   ?record comp:metric ?desiredId.
} ORDER BY ?desiredId

#
# 4.3) What are the Conditions that can be used in conjunction with Measure 39?
#      (where the patient was given an Aspirin at Arrival)
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:measure <http://healthdata.gov/id/measure/39>.
   ?record comp:condition ?desiredId.
} ORDER BY ?desiredId

#
# 4.4) What are the Metrics that can be used in conjunction with Measure 39?
#      (where the patient was given an Aspirin at Arrival)
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:measure <http://healthdata.gov/id/measure/39>.
   ?record comp:metric ?desiredId.
} ORDER BY ?desiredId

#
# 4.5) What are the Conditions that can be used in conjunction with Metric 1?
#      (where the performance was "90th Percentile")
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:metric <http://healthdata.gov/id/metric/1>.
   ?record comp:condition ?desiredId.
} ORDER BY ?desiredId

#
# 4.6) What are the Measures that can be used in conjunction with Metric 1?
#      (where the performance was "90th Percentile")
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:metric <http://healthdata.gov/id/metric/1>.
   ?record comp:measure ?desiredId.
} ORDER BY ?desiredId


#######################################
#
# 5) Find an Id of one type (ConditionId, MeasureId, MetricId)
# that are relevant if BOTH of the other two types are FIXED.
#
#  ?desiredId the info desired
#
#  ?desiredIdPredicate is one of: comp:condition, comp:measure, comp:metric
#  ?givenIdPredicate1 is one of: comp:condition, comp:measure, comp:metric
#  ?givenId1 is an object of ?givenIdPredicate1
#  ?givenIdPredicate2 is one of: comp:condition, comp:measure, comp:metric
#  ?givenId2 is an object of ?givenIdPredicate2
#
#######################################
#
# DO NOT RUN THIS QUERY WITHOUT FILLING IN VALUES FOR:  
# ?givenId1 ?givenIdPedicate1, ?givenId2, ?givenIdPredicate2 and ?desiredIdPredicate
#
#######################################
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record ?givenIdPredicate1 ?givenId1.
   ?record ?givenIdPredicate2 ?givenId2.
   ?record ?desiredIdPredicate ?desiredId.
} ORDER BY ?desiredId

#####################
#
# 5.1 through 5.3 below are sample queries that can be run without modification
#
#####################

#
# 5.1) What are the Metrics that can be used 
#      in conjunction with Condition 2 and Measure 27?
#      (where the patient had a heart failure condition, and died within 30 days)
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:condition <http://healthdata.gov/id/condition/2>.
   ?record comp:measure <http://healthdata.gov/id/measure/27>.
   ?record comp:metric ?desiredId.
} ORDER BY ?desiredId

#
# 5.2) What are the Measures that can be used 
#      in conjunction with Condition 2 and Metric 13?
#      (where the patient had a heart failure condition, 
#      and the hospital mortality rate is the same as the national average)
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:condition <http://healthdata.gov/id/condition/2>.
   ?record comp:metric <http://healthdata.gov/id/metric/13>.
   ?record comp:measure ?desiredId.
} ORDER BY ?desiredId

#
# 5.3) What are the Conditions that can be used 
#      in conjunction with Measure 27 and Metric 13?
#      (where the patient died within 30 days, 
#      and the hospital mortality rate is the same as the national average)
#
PREFIX comp: <http://healthdata.gov/def/compare/>
SELECT DISTINCT ?desiredId WHERE {
   ?record comp:measure <http://healthdata.gov/id/measure/27>.
   ?record comp:metric <http://healthdata.gov/id/metric/13>.
   ?record comp:condition ?desiredId.
} ORDER BY ?desiredId


#######################################
#
# 6) List all HospitalIds and Hospitals. 
#
#      Every HospitalId has exactly one Hospital (description).
#      Use this query to populate a Hashmap, so you don't have to use
#      Virtuoso to get the Hospital from a HospitalId ever again
#      (as it slows down dynamic retrieval in other queries). 
#
#######################################
#
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>    
SELECT DISTINCT ?hospitalId ?hospital WHERE {
   ?hospitalId a hosp:Hospital.
   ?hospitalId rdfs:label ?hospital.
}

#
# 6.1) Count the number of Hospitals 
#
PREFIX hosp: <http://healthdata.gov/def/hospital/>
SELECT (COUNT( ?hospitalId ) as ?numberOfHospitals) WHERE {
   { SELECT DISTINCT ?hospitalId WHERE {
      ?hospitalId a hosp:Hospital. 
   } }
}

#######################################
#
# 7) Retrieve metadata/contact information about a Hospital
#    (Fair Oaks Hospital, in this example)
#
# In this and in all following queries, instances/literals found in the query
# such as <http://healthdata.gov/id/hospital/490101> are merely examples.
# You should replace it with the hospitalId of the hospital for which you want information.
#
#######################################
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX ns: <http://www.w3.org/ns/org#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
SELECT ?lat ?lon ?hospital ?hospitalType ?hasER ?phone ?streetAddr ?city 
       ?stateCode ?countyCode ?zip ?ownershipType WHERE {
    { SELECT ?lat ?lon ?phone ?city ?zip ?streetAddr WHERE {
       <http://healthdata.gov/id/hospital/490101> hosp:site ?site.
       ?site ns:siteAddress ?vcard.
       OPTIONAL { ?vcard vcard:latitude ?lat. }
       OPTIONAL { ?vcard vcard:longitude ?lon. }
       OPTIONAL { ?vcard vcard:tel ?phone. }
       OPTIONAL { ?vcard vcard:locality ?city. }
       OPTIONAL { ?vcard vcard:postal-code ?zip. }
       OPTIONAL { ?vcard vcard:street-address ?streetAddr.  }
    } LIMIT 1 }
    <http://healthdata.gov/id/hospital/490101> rdfs:label ?hospital.
    <http://healthdata.gov/id/hospital/490101> gd:stateCode ?stateId.
    ?stateId rdfs:label ?stateCode.
    <http://healthdata.gov/id/hospital/490101> gd:countyCode ?countyCode.
    <http://healthdata.gov/id/hospital/490101> hosp:ownership ?ownershipType.
    <http://healthdata.gov/id/hospital/490101> hosp:type ?hospitalType.
    <http://healthdata.gov/id/hospital/490101> hosp:emergencyServices ?hasER.  
} LIMIT 1


#######################################
#
# 8) Retrieve all clinical data for a given hospital 
#    (Fair Oaks Hospital, in this example)
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>    
SELECT DISTINCT ?date ?conditionId ?measureId ?metricId ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
   <http://healthdata.gov/id/hospital/490101> hoco:recordset ?rs. 
   {
      GRAPH ?g { ?rs comp:metric  ?metricId. }
      ?g dcterms:issued ?date.
      OPTIONAL { ?rs gd:percentage ?percentage. }
      OPTIONAL { ?rs comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
   } UNION {
      GRAPH ?g { ?rs hoco:record ?record. }
      ?g dcterms:issued ?date.
      OPTIONAL { ?record comp:condition ?conditionId. }
      OPTIONAL { ?record comp:measure ?measureId. }
      OPTIONAL { ?record comp:metric  ?metricId.  }
      OPTIONAL { ?record gd:percentage ?percentage. }
      OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
      OPTIONAL { ?record gd:denominator ?denominator. }
      OPTIONAL { ?record hoco:ratio ?ratio. }
      OPTIONAL { ?record hoco:medianTime ?medianTime. }
      OPTIONAL { ?record hoco:admissions ?admissions. }
      OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
      OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
      OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
      OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
      OPTIONAL { ?record hoco:stateCount ?stateCount. }
      OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
      OPTIONAL { ?record hoco:msdrg ?msdrg. }
   } 
}


#######################################
#
# 8slow) Retrieve all clinical data for a given hospital
#    (Fair Oaks Hospital, in this example)
#
#     Slow version that fetches descriptions for hospitalId, conditionId, measureId, metricId
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>   
SELECT DISTINCT ?date ?condition ?measure ?metric ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?conditionId ?measureId ?metricId ?footnoteId WHERE {
   <http://healthdata.gov/id/hospital/490101> hoco:recordset ?rs. 
   {
      GRAPH ?g { ?rs comp:metric  ?metricId. ?metricId  rdfs:comment   ?metric. }
      ?g dcterms:issued ?date.
      OPTIONAL { ?rs gd:percentage ?percentage. }
      OPTIONAL { ?rs comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
   } UNION {
      GRAPH ?g { ?rs hoco:record ?record. }
      ?g dcterms:issued ?date.
      OPTIONAL { ?record comp:condition ?conditionId. ?conditionId rdfs:comment ?condition. }
      OPTIONAL { ?record comp:measure ?measureId. ?measureId rdfs:comment   ?measure. }
      OPTIONAL { ?record comp:metric  ?metricId. ?metricId  rdfs:comment   ?metric. }
      OPTIONAL { ?record gd:percentage ?percentage. }
      OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
      OPTIONAL { ?record gd:denominator ?denominator. }
      OPTIONAL { ?record hoco:ratio ?ratio. }
      OPTIONAL { ?record hoco:medianTime ?medianTime. }
      OPTIONAL { ?record hoco:admissions ?admissions. }
      OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
      OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
      OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
      OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
      OPTIONAL { ?record hoco:stateCount ?stateCount. }
      OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
      OPTIONAL { ?record hoco:msdrg ?msdrg. }
   } 
}


#######################################
#
# 9) Retrieve all clinical quality averages for a state 
#    (The state of Virginia, in this example)
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?conditionId ?measureId ?metricId ?percentage ?ratio ?medianTime 
      ?stateCount ?nationalCount ?msdrg ?footnote ?footnoteId WHERE {
   <http://reference.data.gov/id/state/VA> rdfs:label ?stateCode.
   <http://reference.data.gov/id/state/VA> hoco:recordset ?rs. 
   GRAPH ?g { ?rs hoco:record ?record. }
   ?g dcterms:issued ?date.
   OPTIONAL { ?record comp:condition ?conditionId.  }
   OPTIONAL { ?record comp:measure ?measureId. }
   OPTIONAL { ?record comp:metric  ?metricId.  }
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
} 


#######################################
#
# 9slow) Retrieve all clinical quality averages for a state 
#    (The state of Virginia, in this example)
#
#     Slow version that fetches descriptions for conditionId, measureId, metricId
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?condition ?measure ?metric ?percentage ?ratio ?medianTime 
      ?stateCount ?nationalCount ?msdrg ?footnote ?conditionId ?measureId ?metricId ?footnoteId WHERE {
   <http://reference.data.gov/id/state/VA> rdfs:label ?stateCode.
   <http://reference.data.gov/id/state/VA> hoco:recordset ?rs. 
   GRAPH ?g { ?rs hoco:record ?record. }
   ?g dcterms:issued ?date.
   OPTIONAL { ?record comp:condition ?conditionId. ?conditionId rdfs:comment ?condition }
   OPTIONAL { ?record comp:measure ?measureId. ?measureId rdfs:comment ?measure }
   OPTIONAL { ?record comp:metric  ?metricId. ?metricId rdfs:comment ?metric }
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
} 


#######################################
#
# 10) Retrieve all clinical quality averages for the US
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?conditionId ?measureId ?metricId ?percentage ?ratio ?medianTime 
      ?nationalCount ?msdrg ?footnote ?footnoteId WHERE {
   <http://reference.data.gov/id/country/US> hoco:recordset ?rs. 
   GRAPH ?g { ?rs hoco:record ?record. }
   ?g dcterms:issued ?date.
   OPTIONAL { ?record comp:condition ?conditionId. }
   OPTIONAL { ?record comp:measure ?measureId. }
   OPTIONAL { ?record comp:metric  ?metricId. }
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
}


#######################################
#
# 10slow) Retrieve all clinical quality averages for the US
#
#     Slow version that fetches descriptions for conditionId, measureId, metricId
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?condition ?measure ?metric ?percentage ?ratio ?medianTime 
      ?nationalCount ?msdrg ?footnote ?conditionId ?measureId ?metricId 
      ?footnoteId WHERE {
   <http://reference.data.gov/id/country/US> hoco:recordset ?rs. 
   GRAPH ?g { ?rs hoco:record ?record. }
   ?g dcterms:issued ?date.
   OPTIONAL { ?record comp:condition ?conditionId. ?conditionId rdfs:comment ?condition. }
   OPTIONAL { ?record comp:measure ?measureId. ?measureId rdfs:comment   ?measure. }
   OPTIONAL { ?record comp:metric  ?metricId. ?metricId  rdfs:comment   ?metric. }
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
}


#######################################
#
# 11) Retrieve the values for a Condition, Measure, and Metric at a given hospital 
#
#     In this example: Where the patient was admitted with outpatient AMI/Chest Pain, 
#     what is the median transfer time to another facility for Acute Coronary Intervention?
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
   {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490101>  hoco:recordset ?rs. }
     ?rs hoco:record ?record.
   } UNION {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490101>  hoco:recordset ?record. }
   }
   ?g dcterms:issued ?date.
   ?record comp:condition <http://healthdata.gov/id/condition/9>.
   ?record comp:measure <http://healthdata.gov/id/measure/38>.  
#          ?record comp:metric <>.                    # No metric filter in this example
   OPTIONAL { ?record gd:percentage ?percentage.}
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
   OPTIONAL { ?record gd:denominator ?denominator. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:admissions ?admissions. }
   OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
   OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
   OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
   OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
} 


#######################################
#
# 12) Retrieve the values for a condition/measure/metric for a state 
#
#     In this example: According to survey, what percentage of patients 
#     felt they always had communication with doctors in the state of Virginia? 
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?stateCode 
# ?condition                  # No condition filter in this example
      ?measure ?metric ?percentage ?ratio ?medianTime 
      ?stateCount ?nationalCount ?msdrg ?footnote ?footnoteId WHERE {
   <http://reference.data.gov/id/state/VA> rdfs:label ?stateCode.
   GRAPH ?g { <http://reference.data.gov/id/state/VA>  hoco:recordset ?rs. }
   ?g dcterms:issued ?date.
   ?rs hoco:record ?record.
#     ?record comp:condition <>.                 # No condition filter in this example
#     <> rdfs:comment ?condition.                # No condition filter in this example
   ?record comp:measure <http://healthdata.gov/id/measure/51>. 
   <http://healthdata.gov/id/measure/51> rdfs:comment   ?measure. 
   ?record comp:metric  <http://healthdata.gov/id/metric/20>. 
   <http://healthdata.gov/id/metric/20>  rdfs:comment   ?metric. 
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
}


#######################################
#
# 13) Retrieve the values for a condition/measure/metric for the US
#
#     In this example: According to survey, what is the US average percentage of patients 
#     who checked "Usually" when asked about the responsiveness of the hospital staff.
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date
#     ?condition                        # No condition filter in this example
      ?measure ?metric ?percentage ?ratio ?medianTime 
      ?nationalCount ?msdrg WHERE {
   <http://reference.data.gov/id/country/US> hoco:recordset ?rs. 
   GRAPH ?g { ?rs hoco:record ?record. }
   ?g dcterms:issued ?date.
#     ?record comp:condition <>.                 # No condition filter in this example
#     <> rdfs:comment ?condition.                # No condition filter in this example
   ?record comp:measure <http://healthdata.gov/id/measure/52>. 
   <http://healthdata.gov/id/measure/52> rdfs:comment   ?measure. 
   ?record comp:metric  <http://healthdata.gov/id/metric/19>. 
   <http://healthdata.gov/id/metric/19>  rdfs:comment   ?metric. 
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
} 


#######################################
#
# 14) Retrieve all data publication dates
#
# All queries greater than 14 require a publication date.
# Presumably, a developer might use this query to drive a script in a loop
#
#######################################
#
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX void: <http://rdfs.org/ns/void#>
SELECT DISTINCT ?date  WHERE {
  ?s void:dataset ?ds .
  ?ds dcterms:issued ?date.
}

#######################################
#
# 15) Retrieve the hospital with the Highest value 
# for some condition/measure/metric for some date published. 
#
# In this example: As reported for June 7, 2011, what hospital performed the greatest 
# percentage of MRIs performed for outpatients suffering from lower back pain?
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX void: <http://rdfs.org/ns/void#>
SELECT DISTINCT ?hospitalId ?hospital ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
#
#     Find the hospital with the highest value
#
  { SELECT DISTINCT ?bag ?hospitalId ?percentage WHERE {
      ?bag comp:condition <http://healthdata.gov/id/condition/9>. 
      ?bag comp:measure <http://healthdata.gov/id/measure/43>.
#        ?bag comp:metric <http://healthdata.gov/id/metric/>. # This query doesn't use a metric
      GRAPH ?g { ?bag gd:percentage ?percentage. }
      ?g dcterms:issued "2011-06-07"^^xsd:date.
      {
         ?rs hoco:record ?bag.
         ?hospitalId  hoco:recordset ?rs.
      } UNION {
         ?hospitalId  hoco:recordset ?bag.
      }
   } ORDER BY DESC( ?percentage ) LIMIT 1 }
#
#     Now retrieve all the information about that performance at that hospital
#
   ?hospitalId  rdfs:label ?hospital.
   OPTIONAL { ?bag comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. } 
   OPTIONAL { ?bag gd:denominator ?denominator. }
   OPTIONAL { ?bag hoco:ratio ?ratio. }
   OPTIONAL { ?bag hoco:medianTime ?medianTime. }
   OPTIONAL { ?bag hoco:admissions ?admissions. }
   OPTIONAL { ?bag hoco:rsmrLow ?rsmrLow. }
   OPTIONAL { ?bag hoco:rsmrHigh ?rsmrHigh. }
   OPTIONAL { ?bag hoco:rsrrLow ?rsrrLow. }
   OPTIONAL { ?bag hoco:rsrrHigh ?rsrrHigh. }
   OPTIONAL { ?bag hoco:stateCount ?stateCount. }
   OPTIONAL { ?bag hoco:medianPayment ?medianPayment. }
   OPTIONAL { ?bag hoco:msdrg ?msdrg. }
}

#######################################
#
# 16) Retrieve the hospital with the lowest value 
# for some condition/measure/metric of ANY date published. 
#
# In this example: What hospital had the overall least  
# percentage of MRIs performed for outpatients suffering from lower back pain?
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
SELECT DISTINCT ?date ?hospitalId ?hospital ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
#
#     Find the hospital with the lowest value for some CMM for any date
#
  { SELECT DISTINCT ?date ?bag ?hospitalId ?percentage WHERE {
      ?bag comp:condition <http://healthdata.gov/id/condition/9>. 
      ?bag comp:measure <http://healthdata.gov/id/measure/43>.
#        ?bag comp:metric <http://healthdata.gov/id/metric/>.
      GRAPH ?g { ?bag gd:percentage ?percentage. }
      ?g dcterms:issued ?date.
      {
         ?rs hoco:record ?bag.
         ?hospitalId  hoco:recordset ?rs.
         ?hospitalId a hosp:Hospital.
      } UNION {
         ?hospitalId  hoco:recordset ?bag.
         ?hospitalId a hosp:Hospital.
      }
   } ORDER BY ASC( ?percentage ) LIMIT 1 }
#
#     Now retrieve all related information about that hospital record
#
   ?hospitalId  rdfs:label ?hospital.
   OPTIONAL { ?bag comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. } 
   OPTIONAL { ?bag gd:denominator ?denominator. }
   OPTIONAL { ?bag hoco:ratio ?ratio. }
   OPTIONAL { ?bag hoco:medianTime ?medianTime. }
   OPTIONAL { ?bag hoco:admissions ?admissions. }
   OPTIONAL { ?bag hoco:rsmrLow ?rsmrLow. }
   OPTIONAL { ?bag hoco:rsmrHigh ?rsmrHigh. }
   OPTIONAL { ?bag hoco:rsrrLow ?rsrrLow. }
   OPTIONAL { ?bag hoco:rsrrHigh ?rsrrHigh. }
   OPTIONAL { ?bag hoco:stateCount ?stateCount. }
   OPTIONAL { ?bag hoco:medianPayment ?medianPayment. }
   OPTIONAL { ?bag hoco:msdrg ?msdrg. }
}

#######################################
#
# 16.1) Retrieve the hospital with the lowest value with sufficient sample size
# for some condition/measure/metric of ANY date published. 
#
# In this example: What hospital had the overall least percentage of MRIs 
# performed, where there is no note indicating insuffient sample size,
# for outpatients suffering from lower back pain?
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
SELECT DISTINCT ?date ?hospitalId ?hospital ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
#
#     Find the hospital with the lowest value for some CMM 
#     where there is no note indicating an insufficient sample.
#
  { SELECT DISTINCT ?date ?bag ?hospitalId ?percentage WHERE {
      ?bag comp:condition <http://healthdata.gov/id/condition/9>. 
      ?bag comp:measure <http://healthdata.gov/id/measure/43>.
#        ?bag comp:metric <http://healthdata.gov/id/metric/>.
      GRAPH ?g { ?bag gd:percentage ?percentage. }
      ?g dcterms:issued ?date.
      {
         ?rs hoco:record ?bag.
         ?hospitalId  hoco:recordset ?rs.
         ?hospitalId a hosp:Hospital.
         OPTIONAL { ?bag comp:footnote ?footId. }
         FILTER ( ! bound( ?footId ) OR ?footId != <http://healthdata.gov/id/footnote/1> )
      } UNION {
         ?hospitalId  hoco:recordset ?bag.
         ?hospitalId a hosp:Hospital.
         OPTIONAL { ?bag comp:footnote ?footId. }
         FILTER ( ! bound( ?footId ) OR ?footId != <http://healthdata.gov/id/footnote/1> )
      }
   } ORDER BY ASC( ?percentage ) LIMIT 1 }
#
#     Now retrieve all information about that record at that hospital
#
   ?hospitalId  rdfs:label ?hospital.
   OPTIONAL { ?bag comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. } 
   OPTIONAL { ?bag gd:denominator ?denominator. }
   OPTIONAL { ?bag hoco:ratio ?ratio. }
   OPTIONAL { ?bag hoco:medianTime ?medianTime. }
   OPTIONAL { ?bag hoco:admissions ?admissions. }
   OPTIONAL { ?bag hoco:rsmrLow ?rsmrLow. }
   OPTIONAL { ?bag hoco:rsmrHigh ?rsmrHigh. }
   OPTIONAL { ?bag hoco:rsrrLow ?rsrrLow. }
   OPTIONAL { ?bag hoco:rsrrHigh ?rsrrHigh. }
   OPTIONAL { ?bag hoco:stateCount ?stateCount. }
   OPTIONAL { ?bag hoco:medianPayment ?medianPayment. }
   OPTIONAL { ?bag hoco:msdrg ?msdrg. }
}


#######################################
#
# 17) Retrieve the state that performs the highest percentage for some
#  condition/measure/metric for a date published. 
#
# In this example: What state had the overall highest percentage of MRIs performed  
# outpatients suffering from lower back pain, as published on June 7, 2011?
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?stateCode ?stateId 
      ?percentage ?ratio ?medianTime 
      ?stateCount ?nationalCount ?msdrg ?footnote ?footnoteId WHERE {
#
#     First find the state with the highest percentage for some CMM on some date
#
  { SELECT DISTINCT ?stateId ?percentage ?record WHERE {
     ?stateId a gd:State.
     GRAPH ?g { ?stateId  hoco:recordset ?rs. }
     ?g dcterms:issued "2011-06-07"^^xsd:date.
     ?rs hoco:record ?record.
     ?record comp:condition <http://healthdata.gov/id/condition/9>.
     ?record comp:measure <http://healthdata.gov/id/measure/43>. 
     ?record gd:percentage ?percentage.
#      ?record comp:metric  <>. 
   } ORDER BY DESC( ?percentage ) LIMIT 1 }
#
#     Now retrieve all other information about that state statistic
#
   ?stateId rdfs:label ?stateCode.
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
}


#######################################
#
# 18) Retrieve the Hospital that has the highest value for a 
# condition/measure/metric within a state, for a publication date.
#
# In this example: In Virginia, for June 7, 2011, what hospital had the highest  
# percentage of MRIs performed for outpatients suffering from lower back pain?
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?hospitalId ?hospital ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
#
#     Find the hospital that had the highest percentage for some CMM on a date
#
{ SELECT DISTINCT ?hospitalId ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
   ?hospitalId a hosp:Hospital.
   ?hospitalId gd:stateCode <http://reference.data.gov/id/state/VA>. 
   GRAPH ?g { ?hospitalId  hoco:recordset ?rs. }
   ?g dcterms:issued "2011-06-07"^^xsd:date.
   {
       ?rs hoco:record ?record.
       ?record comp:condition <http://healthdata.gov/id/condition/9>. 
       ?record comp:measure <http://healthdata.gov/id/measure/43>.  
#          ?record comp:metric <>.                    # No metric filter in this example
       OPTIONAL { ?record gd:percentage ?percentage.}
       OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. } 
       OPTIONAL { ?record gd:denominator ?denominator. }
       OPTIONAL { ?record hoco:ratio ?ratio. }
       OPTIONAL { ?record hoco:medianTime ?medianTime. }
       OPTIONAL { ?record hoco:admissions ?admissions. }
       OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
       OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
       OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
       OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
       OPTIONAL { ?record hoco:stateCount ?stateCount. }
       OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
       OPTIONAL { ?record hoco:msdrg ?msdrg. }
   } UNION {
       ?rs comp:condition <http://healthdata.gov/id/condition/9>. 
       ?rs comp:measure <http://healthdata.gov/id/measure/43>.
#          ?rs comp:metric <>.                        # No metric filter in this example   
       OPTIONAL { ?rs gd:percentage ?percentage.}
       OPTIONAL { ?rs comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. } 
   } 
} ORDER BY DESC( ?percentage ) LIMIT 1 }
?hospitalId  rdfs:label ?hospital.
} LIMIT 1


#######################################    
#
# 19) what is the national data on some condition/measure/metric, 
# and how does my state compare?
#
# In this example: Compare the Survey results of Virginia and US averages over time;
# What percentage of patients responded "Usually" when asked about communication with doctors. 
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?place 
#     ?condition                        # No condition filter in this example
      ?measure ?metric ?percentage ?ratio ?medianTime 
      ?nationalCount ?msdrg WHERE {
#
#     Compare US and VA averages
#
   {
     <http://reference.data.gov/id/country/US> hoco:recordset ?rs. 
   } UNION {
     <http://reference.data.gov/id/state/VA>  hoco:recordset ?rs.
   }
   GRAPH ?g { ?rs hoco:record ?record. }
   ?place hoco:recordset ?rs.
   ?g dcterms:issued ?date.
#
#     Select only the CMM we care about
   
#     ?record comp:condition <>.                 # No condition filter in this example
#     <> rdfs:comment ?condition.                # No condition filter in this example
   ?record comp:measure <http://healthdata.gov/id/measure/51>. 
   <http://healthdata.gov/id/measure/51> rdfs:comment   ?measure. 
   ?record comp:metric  <http://healthdata.gov/id/metric/19>. 
   <http://healthdata.gov/id/metric/19>  rdfs:comment   ?metric. 
#
#     Now get the rest of the information about these statistics
#
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
} ORDER BY ?date


#######################################
#
# 20) what is the national data on this condition/measure/metric, 
# and how does my hospital compare?
#
# In this example: Compare the Survey results from Fair Oaks Hospital and US averages over time;
# What percentage of patients responded "Usually" when asked about communication with doctors. 
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?place 
#     ?condition                        # No condition filter in this example
      ?measure ?metric ?percentage ?footnote ?denominator ?ratio ?medianTime 
      ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount     
      ?nationalCount ?medianPayment ?msdrg ?footnoteId WHERE {
#
#     Compare the US average with a hospital
#
   {
     <http://reference.data.gov/id/country/US> hoco:recordset ?rs. 
     GRAPH ?g { ?rs hoco:record ?record. }
     ?place hoco:recordset ?rs.
   } UNION {
     <http://healthdata.gov/id/hospital/490101>  hoco:recordset ?rs.
     GRAPH ?g { ?rs hoco:record ?record. }
     ?place hoco:recordset ?rs.
   } UNION {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490101>  hoco:recordset ?record. }
     ?place hoco:recordset ?record.
   }
   ?g dcterms:issued ?date.
#
#     Select only the CMM we care about
#

#     ?record comp:condition <>.                 # No condition filter in this example
#     <> rdfs:comment ?condition.                # No condition filter in this example
   ?record comp:measure <http://healthdata.gov/id/measure/51>. 
   <http://healthdata.gov/id/measure/51> rdfs:comment   ?measure. 
   ?record comp:metric  <http://healthdata.gov/id/metric/19>. 
   <http://healthdata.gov/id/metric/19>  rdfs:comment   ?metric. 
#
#     Now retrieve the rest of the information about these statistics
#
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
   OPTIONAL { ?record gd:denominator ?denominator. }
   OPTIONAL { ?record hoco:admissions ?admissions. }
   OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
   OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
   OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
   OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
} ORDER BY ?date


#######################################
#
# 21) of the 3 hospitals in my area, what are their stats on this condition/measure/metric? 
#
# Presumably, some other script will figure out what 3 hospitals are near the user.
#
# In this example: Compare the median time to transfer to another facility for accute
# coronary intervention for the local hospitals: Fair Oaks, Fairfax, and Reston
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?date ?hospital ?condition ?measure 
# ?metric
      ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?hospitalId ?footnoteId WHERE {
#
#     Specify the hospitals
#
   {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490101>  hoco:recordset ?rs. }
     ?hospitalId hoco:recordset ?rs.
     ?rs hoco:record ?record.
   } UNION {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490101>  hoco:recordset ?record. }
     ?hospitalId hoco:recordset ?record.
   } UNION {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490107>  hoco:recordset ?rs. }
     ?hospitalId hoco:recordset ?rs.
     ?rs hoco:record ?record.
   } UNION {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490107>  hoco:recordset ?record. }
     ?hospitalId hoco:recordset ?record.
   } UNION {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490063>  hoco:recordset ?rs. }
     ?hospitalId hoco:recordset ?rs.
     ?rs hoco:record ?record.
   } UNION {
     GRAPH ?g { <http://healthdata.gov/id/hospital/490063>  hoco:recordset ?record. }
     ?hospitalId hoco:recordset ?record.
   }
   ?g dcterms:issued ?date.
#
#     Select only the CMM we are interested in
#
   ?record comp:condition <http://healthdata.gov/id/condition/9>.
   <http://healthdata.gov/id/condition/9> rdfs:comment ?condition.
   ?record comp:measure <http://healthdata.gov/id/measure/38>.
   <http://healthdata.gov/id/measure/38> rdfs:comment  ?measure.
#          ?record comp:metric <>.                    # No metric filter in this example
#          <>  rdfs:comment  ?metric.                 # No metric filter in this example
   ?hospitalId rdfs:label ?hospital.
#
#     Now retrieve the rest of the information about these records from these hospitals
#
   OPTIONAL { ?record gd:percentage ?percentage.}
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
   OPTIONAL { ?record gd:denominator ?denominator. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:admissions ?admissions. }
   OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
   OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
   OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
   OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
} ORDER BY ?date 


#######################################
#
# 22) Retrieve all metadata/contact information for the hospital in some state
# that is highest for some condition/measure/metric on a given publication date.
#
# THIS QUERY TIMES OUT.
# 
# Instead, a developer will have to execute query #18 to obtain the hospitalId.
# And then execute query #7 to get the information about the hospital
#######################################
#
PREFIX ns: <http://www.w3.org/ns/org#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?lat ?lon ?hospital ?hospitalType ?hasER ?phone ?streetAddr ?city 
       ?stateCode ?countyCode ?zip ?ownershipType ?hospitalId 
       ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
{ SELECT ?hospitalId ?hospital ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
#
#     Find the hospital that had the highest percentage for some CMM on a date
#
{ SELECT DISTINCT ?hospitalId ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
   ?hospitalId a hosp:Hospital.
   ?hospitalId gd:stateCode <http://reference.data.gov/id/state/VA>. 
   GRAPH ?g { ?hospitalId  hoco:recordset ?rs. }
   ?g dcterms:issued "2011-06-07"^^xsd:date.
   {
       ?rs hoco:record ?record.
       ?record comp:condition <http://healthdata.gov/id/condition/9>. 
       ?record comp:measure <http://healthdata.gov/id/measure/43>.  
#          ?record comp:metric <>.                    # No metric filter in this example
       OPTIONAL { ?record gd:percentage ?percentage.}
       OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. } 
       OPTIONAL { ?record gd:denominator ?denominator. }
       OPTIONAL { ?record hoco:ratio ?ratio. }
       OPTIONAL { ?record hoco:medianTime ?medianTime. }
       OPTIONAL { ?record hoco:admissions ?admissions. }
       OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
       OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
       OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
       OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
       OPTIONAL { ?record hoco:stateCount ?stateCount. }
       OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
       OPTIONAL { ?record hoco:msdrg ?msdrg. }
   } UNION {
       ?rs comp:condition <http://healthdata.gov/id/condition/9>. 
       ?rs comp:measure <http://healthdata.gov/id/measure/43>.
#          ?rs comp:metric <>.                        # No metric filter in this example   
       OPTIONAL { ?rs gd:percentage ?percentage.}
       OPTIONAL { ?rs comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. } 
   } 
} ORDER BY DESC( ?percentage ) LIMIT 1 }
?hospitalId  rdfs:label ?hospital.
} LIMIT 1 }
{ SELECT ?lat ?lon ?phone ?city ?zip ?streetAddr WHERE {
       ?hospitalId hosp:site ?site.
       ?site ns:siteAddress ?vcard.
       OPTIONAL { ?vcard vcard:latitude ?lat. }
       OPTIONAL { ?vcard vcard:longitude ?lon. }
       OPTIONAL { ?vcard vcard:tel ?phone. }
       OPTIONAL { ?vcard vcard:locality ?city. }
       OPTIONAL { ?vcard vcard:postal-code ?zip. }
       OPTIONAL { ?vcard vcard:street-address ?streetAddr.  }
} LIMIT 1 }
    ?hospitalId rdfs:label ?hospital.
    ?hospitalId gd:stateCode ?stateId.
    ?stateId rdfs:label ?stateCode.
    ?hospitalId gd:countyCode ?countyCode.
    ?hospitalId hosp:ownership ?ownershipType.
    ?hospitalId hosp:type ?hospitalType.
    ?hospitalId hosp:emergencyServices ?hasER.  
} LIMIT 1 


#######################################
#
# 23) Retrieve all clinical information available,
# about the hospital that had the best performance on a 
# condition/measure/metric on a publication date.
#
# In this example: In Virginia, for June 7, 2011, what hospital had the highest  
# percentage of MRIs performed for outpatients suffering from lower back pain?
# And retrieve all clinical information for that hospital on that publication date.
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?date ?hospitalId ?conditionId ?measureId ?metricId 
      ?percentage ?footnote ?denominator
      ?ratio ?medianTime ?admissions ?rsmrLow ?rsmrHigh ?rsrrLow ?rsrrHigh ?stateCount
      ?medianPayment ?msdrg ?footnoteId WHERE {
#
#       Find the hospital that had the best performance on a 
#       condition/measure/metric on a publication date. 
#
  { SELECT  ?hospitalId WHERE {
      ?bag comp:condition <http://healthdata.gov/id/condition/9>. 
      ?bag comp:measure <http://healthdata.gov/id/measure/43>.
#        ?bag comp:metric <http://healthdata.gov/id/metric/>.
      GRAPH ?g { ?bag gd:percentage ?percentage. }
      ?g dcterms:issued "2011-06-07"^^xsd:date.
      {
         ?rs hoco:record ?bag.
         ?hospitalId  hoco:recordset ?rs.
         ?hospitalId  a    hosp:Hospital.
      } UNION {
         ?hospitalId  hoco:recordset ?bag.
         ?hospitalId  a    hosp:Hospital.
      }
   } ORDER BY DESC( ?percentage ) LIMIT 1 }
#
#       Retrieve all clinical information about that hospital
#   
   ?hospitalId hoco:recordset ?rs. 
   {
      GRAPH ?g { ?rs comp:metric  ?metricId. }
      ?g dcterms:issued ?date.
      OPTIONAL { ?rs gd:percentage ?percentage. }
      OPTIONAL { ?rs comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
   } UNION {
      GRAPH ?g { ?rs hoco:record ?record. }
      ?g dcterms:issued ?date.
      OPTIONAL { ?record comp:condition ?conditionId. }
      OPTIONAL { ?record comp:measure ?measureId. }
      OPTIONAL { ?record comp:metric  ?metricId.  }
      OPTIONAL { ?record gd:percentage ?percentage. }
      OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
      OPTIONAL { ?record gd:denominator ?denominator. }
      OPTIONAL { ?record hoco:ratio ?ratio. }
      OPTIONAL { ?record hoco:medianTime ?medianTime. }
      OPTIONAL { ?record hoco:admissions ?admissions. }
      OPTIONAL { ?record hoco:rsmrLow ?rsmrLow. }
      OPTIONAL { ?record hoco:rsmrHigh ?rsmrHigh. }
      OPTIONAL { ?record hoco:rsrrLow ?rsrrLow. }
      OPTIONAL { ?record hoco:rsrrHigh ?rsrrHigh. }
      OPTIONAL { ?record hoco:stateCount ?stateCount. }
      OPTIONAL { ?record hoco:medianPayment ?medianPayment. }
      OPTIONAL { ?record hoco:msdrg ?msdrg. }
   } 
}

#######################################
#
# 24) Retrieve all data for the state that is highest 
#  for a condition/measure/metric for a date published
#
# In this example: Search data published on June 7, 2011 for the state 
# with the highest percentage of MRIs performed for outpatients suffering 
# from lower back pain? And return all clinical data averages for that state,
# for June 7, 2011.
#
#######################################
#
PREFIX gd: <http://reference.data.gov/def/govdata/>
PREFIX hosp: <http://healthdata.gov/def/hospital/>
PREFIX hoco: <http://healthdata.gov/def/hospital-compare/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX comp: <http://healthdata.gov/def/compare/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?date ?stateId ?conditionId ?measureId ?metricId ?percentage ?ratio ?medianTime 
      ?stateCount ?nationalCount ?msdrg ?footnote ?footnoteId WHERE {
#
#     Retrieve the state that performs the BEST for some
#     condition/measure/metric for a date published. 
#
  { SELECT DISTINCT ?stateId WHERE {
     ?stateId a gd:State.
     GRAPH ?g { ?stateId  hoco:recordset ?rs. }
     ?g dcterms:issued "2011-06-07"^^xsd:date.
     ?rs hoco:record ?record.
     ?record comp:condition <http://healthdata.gov/id/condition/9>.
     ?record comp:measure <http://healthdata.gov/id/measure/43>. 
     ?record gd:percentage ?percentage.
#      ?record comp:metric  <>.            # this example does not use a metric filter
   } ORDER BY DESC( ?percentage ) LIMIT 1 }
#
#    Retrieve all clinical quality averages for that state 
#
   ?stateId rdfs:label ?stateCode.
   ?stateId hoco:recordset ?rs. 
   GRAPH ?g { ?rs hoco:record ?record. }
   ?g dcterms:issued ?date.
   OPTIONAL { ?record comp:condition ?conditionId.  }
   OPTIONAL { ?record comp:measure ?measureId. }
   OPTIONAL { ?record comp:metric  ?metricId.  }
   OPTIONAL { ?record gd:percentage ?percentage. }
   OPTIONAL { ?record hoco:stateCount ?stateCount. }
   OPTIONAL { ?record hoco:ratio ?ratio. }
   OPTIONAL { ?record hoco:medianTime ?medianTime. }
   OPTIONAL { ?record hoco:nationalCount ?nationalCount. }
   OPTIONAL { ?record hoco:msdrg ?msdrg. }
   OPTIONAL { ?record comp:footnote ?footnoteId. ?footnoteId rdfs:comment ?footnote. }
} 
